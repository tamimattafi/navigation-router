apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'org.jetbrains.dokka'

ext {
    PUBLISH_GROUP_ID = 'com.attafitamim.navigation'
    PUBLISH_VERSION = '1.2.1'
}

task projectSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For Android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure Kotlin libraries, in case you have them
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier.set('javadoc')
    from dokkaJavadoc.outputDirectory
}

tasks.withType(dokkaHtml.getClass()).configureEach {
    pluginsMapConfiguration.set(
        ["org.jetbrains.dokka.base.DokkaBase": """{ "separateInheritedMembers": true}"""]
    )
}

tasks.named("dokkaHtml") {
    def outputDir = new File(projectDir.parent + "/docs")
    outputDirectory.set(outputDir)
}

artifacts {
    archives projectSourcesJar
    archives javadocJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact projectSourcesJar
                artifact javadocJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Navigation libraries for Android and JVM'
                    url = 'https://github.com/tamimattafi/navigation-router'

                    licenses {
                        license {
                            name = 'Library Licence'
                            url = 'https://github.com/tamimattafi/navigation-router/blob/main/LICENSE'
                        }
                    }

                    developers {
                        developer {
                            id = 'attafitamim'
                            name = 'Tamim Attafi'
                            email = 'attafitamim@gmail.com'
                        }
                    }

                    scm {
                        connection = 'scm:git:github.com/tamimattafi/navigation-router.git'
                        developerConnection = 'scm:git:ssh://github.com/tamimattafi/navigation-router.git'
                        url = 'https://github.com/tamimattafi/navigation-router/tree/main'
                    }
                }
            }
        }

        repositories {
            mavenLocal()
            mavenCentral()
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}
